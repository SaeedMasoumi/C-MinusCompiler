Program  -> INCR DeclarationList EOF
DeclarationList   -> DeclarationList Declaration
DeclarationList -> Declaration
Declaration       -> VarDeclaration
Declaration -> FunDeclaration
VarDeclaration    -> int ID SID ;
VarDeclaration -> int ID SSID [ SIZE_IS NUM ] ;
FunDeclaration    -> FunReturnType ( INCR FUN_IN Params ) { LocalDeclarations StatementList DECR } FUN_OUT
FunReturnType     -> int ID FID
FunReturnType  -> void ID FID
Params            -> ParamList
Params  ->  void
ParamList         ->  ParamList , Param
ParamList -> Param
Param             -> int ID PARAMID
Param  -> int ID PARAMID [ ]
CompoundStmt      -> { INCR LocalDeclarations StatementList DECR }
LocalDeclarations -> LocalDeclarations VarDeclaration
LocalDeclarations  -> EPS
StatementList     -> StatementList Statement
StatementList  ->   EPS
Statement         -> ExpressionStmt
Statement -> CompoundStmt
Statement -> SelectionStmt
Statement ->  IterationStmt
Statement ->  ReturnStmt
ExpressionStmt    ->  Expression ;
ExpressionStmt  -> ;
SelectionStmt     -> if ( Expression SAVE ) Statement else JPF_SAVE Statement JP
IterationStmt     -> while ( LABEL Expression SAVE ) Statement WHILE
ReturnStmt        ->  return GOBACKN ;
ReturnStmt  ->  return Expression RETURN GOBACK ;
Expression        ->  Var = Expression ASSIGN
Expression  ->  SimpleExpression
Var               -> ID PID
Var  -> ID PIDA [ Expression ] ARRPID
SimpleExpression   -> AdditiveExpression == AdditiveExpression EQ
SimpleExpression  -> AdditiveExpression < AdditiveExpression LT
SimpleExpression  -> AdditiveExpression
AdditiveExpression  -> AdditiveExpression + Term ADD
AdditiveExpression  -> AdditiveExpression - Term SUB
AdditiveExpression  -> Term
Term              -> Term * Factor MULT
Term  -> Factor
Factor            ->  ( Expression )
Factor -> Var
Factor -> Call
Factor -> IDNUM NUM
Call              -> ID CID ( Args ) CALL
Args              -> ArgList
Args ->  EPS
ArgList           -> ArgList SEEARG , Expression ADD_ARG
ArgList  -> SEEARG Expression ADD_ARG
PID               ->  EPS
PIDA              ->  EPS
ARRPID            -> EPS
ASSIGN            ->  EPS
SID               -> EPS
SSID              -> EPS
SIZE_IS          -> EPS
ADD               -> EPS
SUB               -> EPS
MULT              -> EPS
IDNUM              -> EPS
EQ                -> EPS
LT                -> EPS
LABEL             -> EPS
SAVE              -> EPS
WHILE             -> EPS
JPF               -> EPS
JPF_SAVE          -> EPS
JP                -> EPS
FUN_IN             -> EPS
FUN_OUT             -> EPS
FID               -> EPS
PARAMID             -> EPS
CID               -> EPS
ADD_ARG           -> EPS
CALL              -> EPS
GOBACKN           -> EPS
GOBACK            -> EPS
RETURN            -> EPS
SEEARG         -> EPS
INCR         ->  EPS
DECR         -> EPS
