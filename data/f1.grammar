Program           -> INC_SCOPE DeclarationList EOF
DeclarationList   -> DeclarationList Declaration | Declaration
Declaration       -> VarDeclaration | FunDeclaration
VarDeclaration    -> int ID SID ; | int ID SSID [ SET_SIZE NUM ] ;
FunDeclaration    -> FunReturnType ( INC_SCOPE INNER Params ) { LocalDeclarations StatementList DEC_SCOPE } OUTER
FunReturnType     -> int ID FID | void ID FID
Params            -> ParamList | void
ParamList         -> ParamList , Param | Param
Param             -> int ID PARID | int ID PARID [ ]
CompoundStmt      -> { INC_SCOPE LocalDeclarations StatementList DEC_SCOPE }
LocalDeclarations -> LocalDeclarations VarDeclaration | EPS
StatementList     -> StatementList Statement | EPS
Statement         -> ExpressionStmt | CompoundStmt | SelectionStmt | IterationStmt | ReturnStmt | output ( GenExpression OUTPUT )
ExpressionStmt    -> Var = Expression ; ASSIGN | ;
SelectionStmt     -> if ( GenExpression SAVE ) Statement JPF | if ( GenExpression SAVE ) Statement else JPF_SAVE Statement JP
IterationStmt     -> while ( LABEL GenExpression SAVE ) Statement WHILE
ReturnStmt        -> return GOBACKN ; | return GenExpression RETURN GOBACK ;
Var               -> ID PID | ID PIDA [ Expression ] ARRPID
GenExpression     -> RelExpression | Expression
RelExpression     -> RelExpression && RelTerm AND | RelTerm
RelTerm           -> Expression == Expression EQ | Expression < Expression LT
Expression        -> Expression + Term ADD | Expression - Term SUB | Term
Term              -> Term * Factor MULT | Term / Factor DIV | Factor
Factor            -> ( Expression ) | Var | Call | PNUM NUM
Call              -> ID CID ( Args ) CALL
Args              -> ArgList | EPS
ArgList           -> ArgList CHECK_ARG , Expression ADD_ARG | CHECK_ARG Expression ADD_ARG
PID               -> EPS
PIDA              -> EPS
ARRPID            -> EPS
ASSIGN            -> EPS
SID               -> EPS
SSID              -> EPS
SET_SIZE          -> EPS
ADD               -> EPS
SUB               -> EPS
MULT              -> EPS
DIV               -> EPS
PNUM              -> EPS
EQ                -> EPS
LT                -> EPS
LABEL             -> EPS
SAVE              -> EPS
WHILE             -> EPS
JPF               -> EPS
JPF_SAVE          -> EPS
JP                -> EPS
AND               -> EPS
OUTPUT            -> EPS
INNER             -> EPS
OUTER             -> EPS
FID               -> EPS
PARID             -> EPS
CID               -> EPS
ADD_ARG           -> EPS
CALL              -> EPS
GOBACKN           -> EPS
GOBACK            -> EPS
RETURN            -> EPS
CHECK_ARG         -> EPS
INC_SCOPE         -> EPS
DEC_SCOPE         -> EPS
